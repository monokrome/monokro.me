#!/usr/bin/env coffee

{argv} = require 'optimist'

path = require 'path'
fs = require 'fs'
mkdirp = require 'mkdirp'

class BlogHandler
  articlesDirectory: './partials/articles'

  constructor: (@argv) ->
    if argv._.length is 0
      argv._.push 'create'

    commandHandler = @[argv._[0]]

    if not commandHandler?
      process.stderr.write "No blog command found for #{argv._[0]}"
      process.exit 100

    commandHandler.call @

  currentFilename: ->
    date = new Date

    segments = [
      @articlesDirectory,
      date.getFullYear().toString(),
      (date.getMonth() + 1).toString(),
      (date.getDate() + 1).toString() + '.md'
    ]

    path.join.apply @, segments

  create: ->
    currentFilename = @currentFilename()
    directory = path.dirname currentFilename

    if fs.existsSync currentFilename
      console.info "#{currentFilename}"
      return

    mkdirp directory, (err) =>
      if err
        console.error "Could not create file at #{currentFilename}"
        process.exit 110

      todaysDate = (new Date).toString()
      underline = Array(todaysDate.length + 1).join '='

      f = fs.openSync currentFilename, 'w'
      fs.writeSync f, "#{todaysDate}\n#{underline}\n"
      fs.close f

      console.info "#{currentFilename}"

subjects =
  blog: BlogHandler

if argv._.length is 0
  process.stderr.write 'Must provide at least one argument.'
  console.error 'Format: mk command'
  process.exit 1

subject = argv._.shift()

if not subjects[subject]?
  process.stderr.write "Unknown command: #{subject}"
  process.exit 2

handler = new subjects[subject] argv

